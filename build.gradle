plugins {
    id 'java-library'
    id 'maven-publish'
    id 'maven'
}

apply plugin: 'java'

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "7.19.16.2"

group 'com.github.hmcts'

version buildNumber
System.out.println('version = ' +  buildNumber)
def gitRepo = "https://github.com/hmcts/ccd-test-definitions"

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/ccd-test-definitions"
    }
}

// tag::repositories[]
repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url 'https://jitpack.io'
    }
}
// end::repositories[]

ext {
    junitJupiterVersion = '5.2.0'
    junitVintageVersion = '5.2.0'
    definitionConverterMain = "uk.gov.hmcts.befta.dse.ccd.DefinitionConverter"
}

jar {
    baseName = "ccd-test-definitions"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
  testLogging.showStandardStreams = true
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task definitionsToExcel(dependsOn: sourcesJar) {
    group = "Execution"
    description = "Runs DefinitionConverter for JSON to Excel"

    doFirst() {
        fileTree("${project.projectDir}/src/main/resources/uk/gov/hmcts/ccd/test_definitions/valid").matching {
            include "*"
        }.visit { FileVisitDetails details ->
            def name = details.getName()
            def inputPath = "${project.projectDir}/src/main/resources/uk/gov/hmcts/ccd/test_definitions/valid/${name}"
            def outputPath = "${project.projectDir}/src/main/resources/uk/gov/hmcts/ccd/test_definitions/excel_generated"

            logger.quiet("\nConverting ${name} to Excel.\n")

            javaexec {
                classpath = sourceSets.main.runtimeClasspath
                main = definitionConverterMain
                args = [
                        "to-excel",
                        inputPath,
                        "LOCAL",
                        outputPath
                ]
            }
        }
    }

    outputs.upToDateWhen { false }
}

task definitionsToJson(dependsOn: sourcesJar) {
    group = "Execution"
    description = "Runs DefinitionConverter for Excel to JSON"

    doFirst() {
        fileTree("${project.projectDir}/src/main/resources/uk/gov/hmcts/ccd/test_definitions/excel").matching {
            include "*.xlsx"
            exclude "~*.xlsx"
        }.visit { FileVisitDetails details ->
            def name = details.getName()
            def inputPath = "${project.projectDir}/src/main/resources/uk/gov/hmcts/ccd/test_definitions/excel/${name}"
            def outputFolderPath = "${project.projectDir}/src/main/resources/uk/gov/hmcts/ccd/test_definitions/valid/"
            def useJurisdictionName = false;

            // overrides for BEFTA Master
            if (name == 'BEFTA_Master_Definition.xlsx') {
                useJurisdictionName = true;
            }

            logger.quiet("\nConverting ${name} to JSON.\n")

            javaexec {
                classpath = sourceSets.main.runtimeClasspath
                main = definitionConverterMain
                args = [
                    "to-json",
                    inputPath,
                    outputFolderPath,
                    useJurisdictionName
                ]
            }
        }
    }

    outputs.upToDateWhen { false }
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'ccd-test-definitions'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'CCD Test Definitions')
                root.appendNode('name', 'CCD Test Definitions')
                root.appendNode('url', gitRepo)
                root.children().last() + pomConfig
            }
        }
    }
}

wrapper.gradleVersion = '2.10'

dependencies {
    compile group: 'com.github.hmcts', name: 'befta-fw', version: '8.7.10'
}
// end::dependencies[]

task elasticCurlCommands() {
    group = "Help"
    description = "Displays the curl commands required to delete BEFTA case type indexes from ElasticSearch"
    doFirst() {
        getCaseTypes().forEach{
            println "curl -X DELETE http://localhost:9200/${it.toLowerCase()}_cases-000001"
        }
    }
}

def getCaseTypes() {
    def caseTypes = []
    new File("${projectDir}/src/main/resources/uk/gov/hmcts/ccd/test_definitions/valid/").eachDir {
        new File(it.path).eachDir {
            if (it.name != 'common') caseTypes << it.name
        }
    }
    // TODO: Hardcode case types which are only available in spreadsheets and add to the list?
    return caseTypes
}
